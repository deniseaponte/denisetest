-*made change to WHERE LOCATION_SYMBOL IN ( &BRANCH); from WHERE LOCATION_NAME IN ( &BRANCH); 1/29/13
SET WEBARCHIVE=OFF
-* File attendance_metrics_summary.fex
-*--------------------------------------------------------------------
-* File:             attendance_metrics_summary.fex
-* Application Name: Metrics and Performance Management Dashboard
-* Description:      Attendance Metrics Report - Summary
-* Calls:
-*
-* Called by:
-* Input Variables:
-*      &TIME_GRP    Value of first sort field
-*      &MEAS_GRP    Value of the second sort field
-*      &ST_DT       Start Date for current year
-*      &END_DT      End Date for current year
-*      &DIV         Division
-*      &BORO        Borough
-*      &DEPT        Department
-*      &BRANCH      Branch
-*      &C_VS_P      Current VS Previous?
-*
-* Developed by:     Joey Baloun - Information Builders
-* Developed:        06/10/2008
-*--------------------------------------------------------------------
-* Maintenance History
-*--------------------------------------------------------------------
-* mm/dd/yy  F.ltname  - description
-*
-*--------------------------------------------------------------------
-INCLUDE ENVIRONMENT_SETTINGS
-INCLUDE COMMON_DATE_FMT_CONV
-*--------------------------------------------------------------------
-*-SET &ECHO='ON';
SET BYDISPLAY = OFF
SET LINES=999999
-*-DEFAULT &OPEN  = ' ';
-DEFAULT &CLOSE = ' ';
-DEFAULT &DIV = 'FOC_NONE'
-DEFAULT &DEPT = 'FOC_NONE'
-DEFAULT &BORO = 'FOC_NONE'
-DEFAULT &LOCATION_SYMBOL = 'FOC_NONE'
-DEFAULT &ST_DT = 'FOC_NONE'
-DEFAULT &END_DT = 'FOC_NONE'
-DEFAULT &TIME_GRP = 'YEAR'
-DEFAULT &MEAS_GRP = 'BY LOCATION_BOROUGH AS MEAS_GRP'
-DEFAULT &ST_DT = '01/01/2007'
-DEFAULT &END_DT = '02/01/2008'
-DEFAULT &C_VS_P = ''
-DEFAULT &OUTPUT = 'HTML'
-*-? &
-*-EXIT
-SET &LBL1 = IF &TIME_GRP EQ 'MONTH' THEN 'Month' ELSE
-           IF &TIME_GRP EQ 'YEAR' THEN 'Calendar Year' ELSE
-           IF &TIME_GRP EQ 'FISCAL_YR' THEN 'Fiscal Year' ELSE
-        IF &TIME_GRP EQ 'QUARTER' THEN 'Quarter' ELSE ' ' ;
-* CONTROLS TABULAR REPORT COLUMN LABELS
-SET &LBL2 = IF '&MEAS_GRP.EVAL' CONTAINS 'LOCATION_SYMBOL' THEN 'Branch/,Department' ELSE
-           IF '&MEAS_GRP.EVAL' CONTAINS 'LOCATION_BOROUGH' THEN 'Borough' ELSE
-           IF '&MEAS_GRP.EVAL' CONTAINS 'DIV' THEN 'NYPL Sector' ELSE
-        IF '&MEAS_GRP.EVAL' CONTAINS 'LOCATION_NETWORK_DEPT' THEN 'Network/,Center' ELSE '';
-* CONTROLS sub heading labels
-SET &LBL3 = IF '&MEAS_GRP.EVAL' CONTAINS 'LOCATION_SYMBOL' THEN 'Branch/,Department' ELSE
-           IF '&MEAS_GRP.EVAL' CONTAINS 'LOCATION_BOROUGH' THEN 'Borough' ELSE
-           IF '&MEAS_GRP.EVAL' CONTAINS 'DIV' THEN 'NYPL Sector' ELSE
-        IF '&MEAS_GRP.EVAL' CONTAINS 'LOCATION_NETWORK_DEPT' THEN 'Network/,Center' ELSE 'System Total';
-SET &G1 = '&TIME_GRP.EVAL' ;
-SET &G2 = '&MEAS_GRP.EVAL' ;
-* BUILDS SECOND WHERE FIELD FOR DRILL-DOWN
-SET &GB2 = IF &G2 CONTAINS 'LOCATION_SYMBOL' THEN 'LOCATION_SYMBOL' ELSE
-           IF &G2 CONTAINS 'LOCATION_BOROUGH' THEN 'LOCATION_BOROUGH' ELSE
-           IF &G2 CONTAINS 'LOCATION_DIVISION' THEN 'LOCATION_DIVISION' ELSE
-        IF &G2 CONTAINS 'LOCATION_NETWORK_DEPT' THEN 'LOCATION_NETWORK_DEPT'
-   ELSE 'SKIP';
SET NODATA = 'N/A'
SET HOLDLIST = PRINTONLY
-*JOIN CLEAR *
-*JOIN LOCATION_ID IN METRIC_DATA_PROGRAMS TO LOCATION_ID IN METRIC_LOCATIONS AS J1
-*-RUN
DEFINE FILE METRIC_DATA_VISITS
METRIC_DT_MDYY/MDYY=HDATE(METRIC_DT, 'MDYY');
-* THIS SECTION SETS THE DATE GROUPING TO DAILY, MONTHLY, YEARLY, QUARTERLY DEPENDING ON USER INPUT
-*CURRENT PERIOD
TIME_GRP_DT_A/A20=HCNVRT(METRIC_DT, '(HYYMDI)', 20, 'A20');
TIME_GRP_DT_B/A4=EDIT(TIME_GRP_DT_A,'9999$$$$$$');
TIME_GRP_DT_C/A2=EDIT(TIME_GRP_DT_A,'$$$$$99$$$');
TIME_GRP_DT_D/A6=EDIT(TIME_GRP_DT_A,'$$$$$$$$99');
FILTER_DT/A30=TIME_GRP_DT_D||'/'||TIME_GRP_DT_C||'/'||TIME_GRP_DT_B;
-* DAILY
-IF &TIME_GRP EQ 'DAY' THEN GOTO DAILY_GRP ELSE
-IF &TIME_GRP EQ 'MONTH' THEN GOTO MONTHLY_GRP ELSE
-IF &TIME_GRP EQ 'QUARTER' THEN GOTO QUARTER_GRP ELSE
-IF &TIME_GRP EQ 'YEAR' THEN GOTO YEARLY_GRP ELSE
-IF &TIME_GRP EQ 'FISCAL_YR' THEN GOTO FISCALYR_GRP ELSE DT_GRP_END;
-DAILY_GRP
 TIME_GRP_DT_E/A30=TIME_GRP_DT_D||'/'||TIME_GRP_DT_C||'/'||TIME_GRP_DT_B;
-GOTO DT_GRP_END;
-* MONTHLY
-MONTHLY_GRP
 TIME_GRP_DT_E/MtYY=METRIC_DT_MDYY;
-GOTO DT_GRP_END;
-* QUARTERLY
-QUARTER_GRP
TIME_GRP_DT_E/QYY=METRIC_DT_MDYY;
-GOTO DT_GRP_END;
-* YEARLY (CALENDAR)
-YEARLY_GRP
TIME_GRP_DT_E/A30=TIME_GRP_DT_B;
-GOTO DT_GRP_END;
-* YEARLY (FISCAL)
-FISCALYR_GRP
TIME_GRP_DT_E/A30=FY;
-DT_GRP_END
-*
GDATE/MDYY = HDATE(METRIC_DT, 'MDYY');
-*
NOTES_LEN/I4 = ARGLEN(500,COMMENTS,NOTES_LEN);
NOTES_CNT/I4 = IF NOTES_LEN GT 0 THEN 1 ELSE 0 ;
END
TABLE FILE METRIC_DATA_VISITS
SUM VISITS
    TARGET
-IF &C_VS_P EQ 'CUR_ONLY' THEN GOTO NO_PRI2 ;
 PRIOR_VISITS
 PRIOR_TARGET
-NO_PRI2
    NOTES_CNT
BY TIME_GRP_DT_E
-IF &MEAS_GRP EQ 'SKIP' THEN GOTO SKPBYS ;
&MEAS_GRP.EVAL
-SKPBYS
-*
WHERE LOCATION_DIVISION IN ( &DIV ) ;
WHERE LOCATION_BOROUGH IN ( &BORO ) ;
WHERE LOCATION_NETWORK_DEPT IN ( &DEPT);
WHERE LOCATION_SYMBOL IN ( &BRANCH);
-*WHERE GDATE FROM '&ST_DT' TO '&END_DT';
WHERE METRIC_DT FROM DT('&SDT_ALL') TO DT('&EDT_ALL');
ON TABLE HOLD AS STEP_1
END
-RUN
-*--------------------------------------------------------------
DEFINE FILE STEP_1
-INCLUDE COMMON_RUNDATE_SET
END
TABLE FILE STEP_1
ON TABLE SUBFOOT
" "
"Attendance Metrics Report - Summary"
"There are total <SUM.NOTES_CNT notes associated with this data."
"Date Range: <+0>&ST_DT To &END_DT "
"NYPL Sector: <+0>&DIV "
"Borough: <+0>&BORO "
"Network/Center:<+0>&DEPT "
"Branch/Department: <+0>&BRANCH "
"Grouping Date: <+0>&LBL1.EVAL  "
"Grouping Measure: <+0>&LBL3.EVAL "
"Run Date/Time: <RUN_DT_TIME  </1>"
-INCLUDE COMMON_PAGE_FOOTING
-*
SUM VISITS/P20C    AS 'Current, Visits'
    TARGET/P20C   AS 'Current, Visits, Target' NOPRINT
-IF &C_VS_P EQ 'CUR_ONLY' THEN GOTO NO_PRI22 ;
 PRIOR_VISITS/P20C   AS 'Prior, Visits'
    PRIOR_TARGET/P20C  AS 'Prior, Visits, Target' NOPRINT
 COMPUTE DELTA_CVP/P20.2%=((VISITS-PRIOR_VISITS)/PRIOR_VISITS)*100; AS '% Diff:,Current,VS,Prior'
-NO_PRI22
BY TIME_GRP_DT_E AS '&LBL1'
-IF &MEAS_GRP EQ 'SKIP' THEN GOTO SKPBYS2 ;
BY MEAS_GRP AS '&LBL3'
-SKPBYS2
-IF &OUTPUT NE 'PDF' THEN GOTO NO_HEAD ;
ON TABLE SUBHEAD
" "
"</35> "
-NO_HEAD

ON TABLE SET PAGE-NUM NOPAGE
ON TABLE RECOMPUTE
ON TABLE PCHOLD AS 'visits' FORMAT &OUTPUT &CLOSE
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
-INCLUDE COMMON_REPORT_STYLE
-*
-IF &OUTPUT NE 'PDF' THEN GOTO NO_IMG ;
TYPE=TABHEADING, IMAGE=&PDF_FILE, position=(0.05 0.05), SIZE=(11 4.5), $
-*-GOTO NO_DRL;
-NO_IMG
-IF &OUTPUT EQ 'EXL2K' THEN GOTO NO_DRL ;
TYPE=DATA,COLUMN=N1 ,FOCEXEC=attendance_metrics_detail(G1='&G1' G2='&GB2' SDATE=N1 SMEASURE=' ' DIV=&DIV.QUOTEDSTRING BORO=&BORO.QUOTEDSTRING DEPT=&DEPT.QUOTEDSTRING BRANCH=&BRANCH.QUOTEDSTRING END_DT='&END_DT' ST_DT='&ST_DT' OUTPUT='&OUTPUT' C_VS_P='&C_VS_P'),
TARGET='_Blank', $
-IF &MEAS_GRP EQ 'SKIP' THEN GOTO NO_DRL ;
TYPE=DATA,COLUMN=N2 ,FOCEXEC=attendance_metrics_detail(G1='&G1' G2='&GB2' SDATE=N1 SMEASURE=MEAS_GRP DIV=&DIV.QUOTEDSTRING BORO=&BORO.QUOTEDSTRING DEPT=&DEPT.QUOTEDSTRING BRANCH=&BRANCH.QUOTEDSTRING END_DT='&END_DT' ST_DT='&ST_DT' OUTPUT='&OUTPUT' C_VS_P='&C_VS_P'),
TARGET='_Blank', $
-NO_DRL
-*  THIS NAMES THE EXCEL WORKSHEET
-IF &OUTPUT NE 'EXL2K' THEN GOTO FINISH_ST ;
TYPE=REPORT, TITLETEXT='Summary', $
TYPE=REPORT, GRID=OFF, $
-FINISH_ST
TYPE=REPORT, COLUMN=N6, WRAP=1.0,$
ENDSTYLE
END
-RUN
-IF &LINES GT 0 THEN GOTO SKP_MSG ;
-HTMLFORM NODATA
-SKP_MSG
